plugins {
    id 'java'
    id 'idea'
    id 'jacoco'
    id 'application'
    id 'org.web3j' version '4.4.0'
    id "com.diffplug.gradle.spotless" version "3.25.0"
    id 'io.codearte.nexus-staging' version '0.21.1'
    id "de.marcphilipp.nexus-publish" version "0.4.0"
    id 'org.jetbrains.kotlin.jvm' version '1.3.50'
    id "de.undercouch.download" version "4.0.0"
}

description 'Web3j-evm extension'

ext {
    web3jVersion = '4.5.5'
    junitVersion = '5.5.2'
    log4jVersion = '2.12.1'
    guavaVersion = '28.1-jre'
    jacksonVersion = '2.10.0'
}

nexusStaging {
    username System.getenv('OSSRH_USERNAME')
    password System.getenv('OSSRH_PASSWORD')
}

apply {
    [
        'jacoco',
        'java',
        'javadoc',
        'repositories',
        'publish',
        'spotless',
    ].each { buildScript ->
        download {
            src "https://raw.githubusercontent.com/web3j/build-tools/master/gradle/$buildScript/build.gradle"
            dest "$rootDir/gradle/$buildScript/build.gradle"
            overwrite true
            quiet true
            onlyIfModified true
        }
        from("$rootDir/gradle/$buildScript/build.gradle")
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            pom {
                scm {
                    url = "https://github.com/web3j/evm"
                    connection = "scm:https://github.com/web3j/evm.git"
                    developerConnection = "scm:git://github.com/web3j/evm.git"
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "org.web3j:core:$web3jVersion"
    compile "org.apache.logging.log4j:log4j-api:$log4jVersion"
    compile "org.apache.logging.log4j:log4j-core:$log4jVersion"
    compile "com.google.guava:guava:$guavaVersion"
    compile "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
    compile "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    testCompile "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    testCompile "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testCompile "org.junit.jupiter:junit-jupiter-params:$junitVersion"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}

jar {
    from {
        configurations.compile.grep {
            def isBesuLib = it.getParentFile().getName().equals("libs") && (it.getName().startsWith("besu-") || it.getName().startsWith("plugin-"))

            return isBesuLib
        }
        .collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
    }
}

application {
    mainClassName = 'org.web3j.evm.Demo'
}

run {
    standardInput = System.in
}
