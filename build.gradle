plugins {
    id 'java'
    id 'maven-publish'
    id 'idea'
    id 'jacoco'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'com.diffplug.gradle.spotless' version '4.5.1'
    id 'io.codearte.nexus-staging' version '0.21.1'
    id 'de.marcphilipp.nexus-publish' version '0.4.0'
    id 'org.jetbrains.kotlin.jvm' version '1.6.21'
    id 'de.undercouch.download' version '4.0.0'
}

description 'Web3j-evm extension'

ext {
    web3jVersion = '4.10.0'
    log4jVersion = '2.15.0'
    guavaVersion = '28.1-jre'
    jacksonVersion = '2.10.0'
    klaxonVersion = '5.0.1'
    kotlinVersion = '1.6.21'
    besuPluginVersion = '23.4.0'
    besuInternalVersion = '23.4.0'
    besuInternalCryptoVersion = '23.1.3'
    besuCryptoDepVersion = '0.7.1'
}

apply {
    [
        'bintray',
        'jacoco',
        'java',
        'javadoc',
        'repositories',
        'publish',
        'spotless',
        'junit'
    ].each { buildScript ->
        download {
            src "https://raw.githubusercontent.com/web3j/build-tools/master/gradle/$buildScript/build.gradle"
            dest "$rootDir/gradle/$buildScript/build.gradle"
            overwrite false
            quiet true
            onlyIfModified true
        }
        from("$rootDir/gradle/$buildScript/build.gradle")
    }
}

repositories {
    mavenCentral()
    maven { url "https://hyperledger.jfrog.io/artifactory/besu-maven/" }
    maven { url "https://artifacts.consensys.net/public/maven/maven/" }
    maven { url "https://splunk.jfrog.io/splunk/ext-releases-local" }
}

dependencies {
    implementation "org.web3j:core:$web3jVersion"
    implementation "com.google.guava:guava:$guavaVersion"
    implementation "com.beust:klaxon:$klaxonVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"

    def withoutAbi = { exclude group: 'org.web3j', module: 'abi' }

    implementation "org.hyperledger.besu:plugin-api:$besuPluginVersion", withoutAbi
    implementation "org.hyperledger.besu.internal:besu:$besuInternalVersion", withoutAbi
    implementation "org.hyperledger.besu:evm:$besuPluginVersion", withoutAbi
    implementation "org.hyperledger.besu.internal:api:$besuInternalVersion", withoutAbi
    implementation "org.hyperledger.besu.internal:config:$besuInternalVersion", withoutAbi
    implementation "org.hyperledger.besu.internal:core:$besuInternalVersion", withoutAbi
    implementation "org.hyperledger.besu.internal:crypto:$besuInternalCryptoVersion", withoutAbi
    implementation "org.hyperledger.besu.internal:rlp:$besuInternalVersion", withoutAbi
    implementation "org.hyperledger.besu.internal:kvstore:$besuInternalVersion", withoutAbi
    implementation "org.hyperledger.besu.internal:metrics-core:$besuInternalVersion", withoutAbi
    implementation "org.hyperledger.besu.internal:trie:$besuInternalVersion", withoutAbi
    implementation "org.hyperledger.besu.internal:util:$besuInternalVersion", withoutAbi
    implementation "org.hyperledger.besu:bls12-381:$besuCryptoDepVersion", withoutAbi
    implementation "org.hyperledger.besu:secp256k1:$besuCryptoDepVersion", withoutAbi
}


compileKotlin {
    kotlinOptions {
        jvmTarget = "17"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "17"
    }
}

configurations.all {
    resolutionStrategy {
        //failOnVersionConflict()
    }
}

tasks.named("spotlessKotlin").configure { dependsOn("compileKotlin","spotlessJava", "spotlessGroovyGradle","processTestResources") }
tasks.named("spotlessJava").configure { dependsOn("compileTestJava","compileTestKotlin","compileKotlin","compileTestKotlin","spotlessGroovyGradle","processTestResources") }
tasks.named("test").configure { dependsOn("spotlessKotlin","spotlessJava") }


// To publish in mavenLocal()
// tasks.withType(Sign)*.enabled = false
